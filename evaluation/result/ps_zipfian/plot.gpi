set terminal epscairo enhanced size 7in, 2.8in background rgb 'white' font 'Calibri,18'
set output 'output.eps'

set multiplot




N1 = floor(system("wc -l zipfian_30.dat"))
N2 = floor(system("wc -l zipfian_50.dat"))
N3 = floor(system("wc -l zipfian_70.dat"))
N4 = floor(system("wc -l zipfian_90.dat"))
N5 = floor(system("wc -l zipfian_110.dat"))

set key center horizontal Right maxcols 5 at screen 0.5,0.94
set key box samplen 0.4 width 0 height 0.2 spacing 0.8 font 'Calibri,20'

set xrange [1:8192]
set yrange [0:1]


# 그래프 관련 요소 제거
unset border          # 테두리 제거
unset tics            # 축 눈금 제거
unset xlabel          # x축 레이블 제거
unset ylabel          # y축 레이블 제거
unset xtics           # x축 눈금 제거
unset ytics

plot NaN lw 3 title "s=0.3", \
	 NaN lw 3 title "s=0.5", \
	 NaN lw 3 title "s=0.7", \
	 NaN lw 3 title "s=0.9", \
	 NaN lw 3 title "s=1.1"

unset key

set ytics 0.0, 0.2, 1.0 offset 0.5,0 scale 0
set xtics 2000, 2000, 8000 offset 0,0.5 scale 0
set grid y
set border

set ylabel "CDF" offset 2,0 font 'Calibri,20'
set xlabel 'Write Amplif{}ication' offset 0,1 font 'Calibri,20'

set tmargin at screen 0.85

set lmargin at screen 0.11
set rmargin at screen 0.46

plot "zipfian_30.dat" u 1:(1.0/N1) smooth cumulative w steps lw 3 title "s=0.3", \
	 "zipfian_50.dat" u 1:(1.0/N2) smooth cumulative w steps lw 3 title "s=0.5", \
	 "zipfian_70.dat" u 1:(1.0/N3) smooth cumulative w steps lw 3 title "s=0.7", \
	 "zipfian_90.dat" u 1:(1.0/N4) smooth cumulative w steps lw 3 title "s=0.9", \
	 "zipfian_110.dat" u 1:(1.0/N5) smooth cumulative w steps lw 3 title "s=1.1", \

set xtics 10
set xrange [1:40]
set yrange [0:1.0]
set lmargin at screen 0.60
set rmargin at screen 0.95

set xlabel "Read Amplification"
unset ylabel
#unset key

plot "zipfian_30.dat" u 2:(1.0/N1) smooth cumulative w steps lw 3 title "s=0.3", \
	 "zipfian_50.dat" u 2:(1.0/N2) smooth cumulative w steps lw 3 title "s=0.5", \
     "zipfian_70.dat" u 2:(1.0/N3) smooth cumulative w steps lw 3 title "s=0.7", \
     "zipfian_90.dat" u 2:(1.0/N4) smooth cumulative w steps lw 3 title "s=0.9", \
     "zipfian_110.dat" u 2:(1.0/N5) smooth cumulative w steps lw 3 title "s=1.1", \
